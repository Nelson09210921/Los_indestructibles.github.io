class Snake {

    // El juego se juega en 
    field SnakeGame game;

    // Pocision de la cabeza de la serpiente
    field int posX, posY;

    // El largo de la serpiente
    field int length;

    // Que tanto crece
    field int growby;

    // Cuanto ha crecido
    field int togrow;

    // Direccion de la serpiente
    // 0=none,1=up,2=down,3=left,4=right
    field int dir, lastdir;

    // historial de movimimiento
    field Array histX, histY;
    field int histSize;

    // Se crea una nueva serpiente
    constructor Snake new(SnakeGame Agame, int Ax, int Ay, int Alen, int Agrowby) {

        var int i;

        let game = Agame;
        let posX = Ax - Alen;
        let posY = Ay;
        let length = Alen;
        let growby = Agrowby;

        let togrow = 0;
        let dir = 4;
        let lastdir = 4;
        let histSize = 512;
        let histX = Array.new(histSize);
        let histY = Array.new(histSize);

        // Dibuja la serpiente inicial
        let i = 0;
        while( i < length ) {
            let posX = posX + 1;
            do drawHead(game.getCycle());
            let i = i + 1;
            do game.nextCycle();
        }

        return this;

    }

    // Coloca a la serpiente
    method void dispose() {

        do histX.dispose();
        do histY.dispose();
        do Memory.deAlloc(this);
        return;

    }

    
    method boolean checkRewriteHistory() {

        if( game.getCycle() = histSize ) {
            do rewriteHistory();
            return true;
        }

        return false;

    }

    // Re escribe el historial de movimientos
    method void rewriteHistory() {

        var int i, src, dst;

        let i = length;
        let dst = 0;
        let src = histSize - length;
        while( i > 0 ) {
            let histX[dst] = histX[src];
            let histY[dst] = histY[src];
            let src = src + 1;
            let dst = dst + 1;
            let i = i - 1;
        }

        return;

    }

    // obtiene la posicion de la cabeza
    method int posX() {
        return posX;
    }
    method int posY() {
        return posY;
    }

    // obtiene el largo
    method int getLength() {
        return length;
    }

    // obtiene la direccion
    method int getDir() {
        return dir;
    }

    // obtiene la ultima direccion
    method int getLastDir() {
        return lastdir;
    }

    // settea la direccion
    method void setDir(int newdir) {
        let dir = newdir;
        return;
    }

    // copia dir
    method void rememberDir() {
        let lastdir = dir;
        return;
    }

    //direccion actual
    method boolean tryMove() {

        var SnakeGrid grid;
        let grid = game.getGrid();

        if( dir = 1 ) {
            // arriba
            if( posY = 0 ) {
                return false;
            }
            else {
                let posY = posY - 1;
            }
        }
        if( dir = 2 ) {
            // abajo
            if( posY = grid.sizeY() ) {
                return false;
            }
            else {
                let posY = posY + 1;
            }
        }
        if( dir = 3 ) {
            // izquierda
            if( posX = 0 ) {
                return false;
            }
            else {
                let posX = posX - 1;
            }
        }
        if( dir = 4 ) {
            // derecha
            if( posX = grid.sizeX() ) {
                return false;
            }
            else {
                let posX = posX + 1;
            }
        }

        // si la serpiente de choca a si misma
        if( grid.checkOccupied(posX, posY) ) {
            return false;
        }

        return true;

    }

    // crece la serpiente
    method void grow(int cycle) {

        // nueva cabeza
        do drawHead(cycle);

        
        if( togrow > 0 ) {
            let togrow = togrow - 1;
            let length = length + 1;
        }
        
        else {
            do clearTail(cycle);
        }

        return;

    }

    // dibuja la cabeza actual de la serpiente
    method void drawHead(int cycle) {

        var SnakeGrid grid;

        let grid = game.getGrid();
        do grid.drawSnakeBit(posX, posY);
        let histX[cycle] = posX;
        let histY[cycle] = posY;

        return;

    }

    // limpia la cola
    method void clearTail(int cycle) {

        var SnakeGrid grid;
        var int tailIndex, tailX, tailY;

        let tailIndex = cycle - length;
        let tailX = histX[tailIndex];
        let tailY = histY[tailIndex];
        let grid = game.getGrid();
        do grid.clearSnakeBit(tailX, tailY);

        return;

    }

    // serpiente come
    method void eatFood() {
        let togrow = togrow + growby;
        return;
    }

}